import play.gradle.Language
import play.gradle.plugin.PlayPlugin

plugins {
    alias(libs.plugins.twirl)
    alias(libs.plugins.play)
}

play {
    lang = Language.SCALA
}

def scalaVersion = System.getProperty('scala.version', PlayPlugin.DEFAULT_SCALA_VERSION).replaceAll('\\D*$', '')
def server = System.getProperty('backend.server', 'pekko-http').with {if (it == "pekko") "pekko-http" else it }

dependencies {
    implementation platform("org.playframework:play-bom_${scalaVersion}:${libs.versions.play.get()}")

    implementation "org.playframework:play-${server}-server_${scalaVersion}"
    implementation "org.playframework:play-guice_${scalaVersion}"
    implementation "org.playframework:play-logback_${scalaVersion}"
    implementation "net.codingwell:scala-guice_${scalaVersion}:${libs.versions.scala.guice.get()}"
    implementation "io.lemonlabs:scala-uri_${scalaVersion}:${libs.versions.scala.uri.get()}"
    implementation libs.logstash.logback.encoder

    testImplementation "org.playframework:play-test_${scalaVersion}"
    testImplementation "org.playframework:play-filters-helpers_${scalaVersion}"
    testImplementation "org.scalatestplus.play:scalatestplus-play_${scalaVersion}:${libs.versions.scalatestplus.play.get()}"
    testRuntimeOnly "org.scalatestplus:junit-5-10_${scalaVersion}:${libs.versions.scalatestplus.asProvider().get()}"
}

run {
    systemProperties System.properties
}

test {
    useJUnitPlatform {
        includeEngines 'scalatest'
    }
}

// TODO: Remove after release Play Gradle Plugin
repositories {
    mavenCentral()
    maven {
        url = uri("https://oss.sonatype.org/content/repositories/snapshots")
    }
    mavenLocal()
}
