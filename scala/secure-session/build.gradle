import play.gradle.Language
import play.gradle.plugin.PlayPlugin

plugins {
    alias(libs.plugins.twirl)
    alias(libs.plugins.play)
}

play {
    lang = Language.SCALA
}

def scalaVersion = System.getProperty('scala.version', PlayPlugin.DEFAULT_SCALA_VERSION).replaceAll('\\D*$', '')
def server = System.getProperty('backend.server', 'pekko-http').with {if (it == "pekko") "pekko-http" else it }

dependencies {
    implementation platform("org.playframework:play-bom_${scalaVersion}:${libs.versions.play.get()}")

    implementation "org.playframework:play-${server}-server_${scalaVersion}"
    implementation "org.playframework:play-guice_${scalaVersion}"
    implementation "org.playframework:play-filters-helpers_${scalaVersion}"
    implementation "org.playframework:play-logback_${scalaVersion}"
    implementation libs.kalium
    implementation libs.jnr.ffi
    implementation "org.apache.pekko:pekko-distributed-data_${scalaVersion}:${libs.versions.pekko.get()}"
    implementation "org.apache.pekko:pekko-cluster-typed_${scalaVersion}:${libs.versions.pekko.get()}"

    testImplementation "org.scalatestplus.play:scalatestplus-play_${scalaVersion}:${libs.versions.scalatestplus.play.get()}"
    testRuntimeOnly "org.scalatestplus:junit-5-10_${scalaVersion}:${libs.versions.scalatestplus.asProvider().get()}"
}

run {
    systemProperties System.properties
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.additionalParameters = ["-feature"]
}

test {
    useJUnitPlatform {
        includeEngines 'scalatest'
    }
}

// TODO: Remove after release Play Gradle Plugin
repositories {
    mavenCentral()
    maven {
        url = uri("https://oss.sonatype.org/content/repositories/snapshots")
    }
    mavenLocal()
}
